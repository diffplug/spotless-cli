import com.diffplug.spotless.cli.picocli.usage.GenerateUsagePropertiesTask
import com.diffplug.spotless.extra.wtp.EclipseWtpFormatterStep
import org.gradle.plugins.ide.eclipse.model.EclipseWtp

plugins {
	id 'buildlogic.picocli-conventions'
	id 'buildlogic.java-special-tests-conventions'
	id 'buildlogic.spotless-json-conventions'
}

version = rootProject.version


dependencies {
	testImplementation project(':testlib')
	implementation libs.bundles.spotless.libs
	implementation libs.diff.utils

	implementation project(':provisioner')
}

application {
	mainClass = 'com.diffplug.spotless.cli.SpotlessCLI'
	applicationName = 'spotless'
	archivesBaseName = 'spotless-cli'
}

tasks.withType(Test).configureEach {
	if (it.name == 'test' || it.name == 'testNpm' || it.name == 'testSeparateJvm') {
		it.systemProperty 'spotless.cli.inSameThread', 'true' //mark tests
	}
	if (it.name == 'testCliProcess' || it.name == 'testCliProcessNpm') {
		def taskInstallDist = tasks.named('installDist', org.gradle.api.tasks.Sync)
		it.dependsOn(taskInstallDist) //ensure the CLI is built before running tests
		it.systemProperty 'spotless.cli.processLauncher', processLauncherPathIninstallDist(taskInstallDist.get()) //pass the path to the CLI launcher

		def toolchains = project.extensions.getByType(JavaToolchainService)
		def launcher   = toolchains.launcherFor(java.toolchain).get()
		def javaHome = launcher.metadata.installationPath.asFile

		it.systemProperty 'spotless.cli.processLauncher.javaHome', javaHome.absolutePath //pass the path to the JDK to use
	}
}

def processLauncherPathIninstallDist(org.gradle.api.tasks.Sync installDistTask) {
	def binaryPath = installDistTask.destinationDir.absolutePath + "/bin/spotless"
	def suffix = System.getProperty("os.name").toLowerCase().contains("windows") ? ".bat" : ""
	println("Using spotless CLI launcher at: " + binaryPath + suffix)
	return binaryPath + suffix
}

tasks.register("generateUsage", GenerateUsagePropertiesTask) {
	it.outputDir.set(layout.buildDirectory.dir("generated-usages").get().asFile)
}
