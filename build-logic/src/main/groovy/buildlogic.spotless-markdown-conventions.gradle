import com.diffplug.spotless.cli.picocli.usage.DocumentedUsages
import com.diffplug.gradle.spotless.FreshMarkExtension

plugins {
	id 'buildlogic.spotless-common-conventions'
	id 'com.diffplug.spotless-changelog'
}

Action<FreshMarkExtension> freshmarkSetup = new Action<FreshMarkExtension>(){

			void execute(FreshMarkExtension freshmark) {
				freshmark.target '*.md', 'docs/devs/*.md'
				freshmark.properties(new FreshmarkPropertiesAction(rootProject, project))
				freshmark.properties {
					it.put('changeThisValueToForceCacheMiss', '1f31705c-b1c6-4bce-9831-98881431b721')
				}
				freshmark.prettier() // prettier for markdown formatting
				freshmark.endWithNewline()
			}
		}

spotless {
	freshmark {
		// freshmark for keeping markdown files up to date
		freshmarkSetup.execute(it)
	}
}

interface MarkdownProjInjections {
	@javax.inject.Inject
	ExecOperations getExecOperations()
}

// if this freshmark has a changelog file, then it has version-sensitive content
if (tasks.names.contains('changelogCheck')) {
	// create a freshmark apply task manually
	FreshMarkExtension freshmark = new FreshMarkExtension(spotless)
	freshmarkSetup.execute(freshmark)
	// override version for this task, we want to use versionNext because versionLast is bumped after evaluation, so peek ahead
	freshmark.properties {
		it.put('spotlessVersion', rootProject.spotlessChangelog.versionNext)
	}
	def changelogBumpFreshmark = freshmark.createIndependentApplyTask('changelogBumpFreshmark')
	// freshmark should run after the changelog bump
	changelogBumpFreshmark.dependsOn tasks.named('changelogBump')

	def changelogBumpFreshmarkGitAdd = tasks.register('changelogBumpFreshmarkGitAdd') {
		def execOps = project.objects.newInstance(MarkdownProjInjections).execOperations
		// this git add should run after the freshmark
		dependsOn(changelogBumpFreshmark)
		// do the git add
		doLast {
			execOps.exec { commandLine 'git', 'add' , '*.md' }
		}
	}
	tasks.named('changelogPush').configure {
		dependsOn changelogBumpFreshmarkGitAdd
	}
}


class FreshmarkPropertiesAction implements Action<Map<String, Object>> {

	private final Project rootProject
	private final Project project

	FreshmarkPropertiesAction(Project rootProject, Project project) {
		this.rootProject = rootProject
		this.project = project
	}

	@Override
	void execute(Map<String, Object> properties) {
		def versionsCollected = getVersionsCollected()
		properties.putAll(versionsCollected)
		def usageHelps = getUsageHelps()
		properties.putAll(usageHelps)
		properties.put('spotlessVersion', rootProject.spotlessChangelog.versionLast ?: '0.0.0')
	}

	Map<String, String> getVersionsCollected() {
		Map<String, String> versionsCollected = [:]
		def nativeVersions = getLibsNativeVersions()
		versionsCollected.putAll(nativeVersions)
		def bundledVersions = getBundledVersions()
		versionsCollected.putAll(bundledVersions)
		return versionsCollected
	}

	Map<String, String> getLibsNativeVersions() {
		def versionCatalog = project.extensions.getByType(VersionCatalogsExtension).named("libs")
		Map<String, String> versionsCollected = [:]
		versionCatalog.versionAliases.each {versionAlias ->
			versionsCollected.put('libs.versions.' + versionAlias, versionCatalog.findVersion(versionAlias).get().toString())
		}
		return versionsCollected
	}

	Map<String, String> getBundledVersions() {
		Map<String, String> versionsCollected = [:]
		versionsCollected.put('libs.versions.bundled.prettier', com.diffplug.spotless.npm.PrettierFormatterStep.DEFAULT_VERSION)
		return versionsCollected
	}

	Map<String, Object> getUsageHelps() {
		Map<String, Object> usageHelps = [:]
		File outputDir = rootProject.file("app/build/generated-usages")

		DocumentedUsages.values().each { usage ->
			File usageFile = new File(outputDir, usage.fileName)
			usageHelps.put("usage.${usage.formatterStepName}.array".toString(), new UsageHelp(usageFile))
		}
		return usageHelps
	}
}


class UsageHelp implements Serializable {

	private final File usageFile

	UsageHelp(File usageFile) {
		this.usageFile = usageFile
	}

	@Override
	String toString() {
		if (usageFile.canRead()) {
			return asJsArrayOfLines(usageFile.readLines())
		}
		return '[]'
	}


	private static String asJsArrayOfLines(List<String> lines) {
		return '[' +
				lines.collect() { line -> "'${jsEscaped(line)}'" }
				.join(',\n') +
				']'
	}

	private static String jsEscaped(String str) {
		return str.replace('\\', '\\\\').replace("'", "\\'")
	}
}

tasks.named('spotlessFreshmark').configure {
	it.mustRunAfter(tasks.withType(Test))
	it.mustRunAfter(project(':app').tasks.withType(Test))

	it.dependsOn(project(':app').tasks.named('generateUsage'))
	it.inputs.dir('app/build/generated-usages')
}
