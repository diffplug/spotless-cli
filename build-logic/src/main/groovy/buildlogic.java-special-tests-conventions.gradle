plugins {
	id 'java'
	id 'com.adarshr.test-logger'
}

// See com.diffplug.spotless.tag package for available JUnit 5 @Tag annotations
def special = [
	'npm',
	'cliProcess',
	'cliProcessNpm',
	'cliNative',
	'cliNativeNpm',
	'separateJvm'
]

tasks.named('test', Test).configure {
	useJUnitPlatform()
}

special.forEach { tag ->
	tasks.register("test${tag.capitalize()}", Test) {
		useJUnitPlatform {
			includeTags tag
			excludeTags 'none()'
		}
	}
}

tasks.named('testSeparateJvm', Test) {
	it.forkEvery = 1 // ensure that each test task runs in a separate JVM
}

tasks.withType(Test).configureEach { Test task ->
	// any test task that does not end with Npm should exclude all the npm tests
	if (!task.name.toLowerCase().endsWith('npm')) {
		def toExclude = special.findAll { it.toLowerCase().endsWith('npm') }.join('|')
		task.useJUnitPlatform {
			excludeTags toExclude
		}
	}
	// any test task that does not end with separateJvm should exclude all the separateJvm tests
	if (!task.name.toLowerCase().endsWith('separatejvm')) {
		task.useJUnitPlatform {
			excludeTags 'separateJvm'
		}
	}
}

tasks.register('testAll') {
	dependsOn tasks.matching { it.name.startsWith('test') && it.name != 'testAll' }
}

def testAllCliProcess = tasks.register('testAllCliProcess') {
	dependsOn tasks.matching { it.name.startsWith('testCliProcess') }
}

tasks.named('check').configure {
	it.dependsOn(testAllCliProcess)
}

[
	tasks.named('testCliNative'),
	tasks.named('testCliNativeNpm')
].each {
	it.configure { Test task ->
		task.inputs.files(rootProject.project(':app').tasks.named('nativeCompile').get().outputs.files)
	}
}

tasks.register('testAllCliNative') {
	dependsOn tasks.matching { it.name.startsWith('testCliNative') }
}

tasks.withType(Test).configureEach {
	testLogging.showStandardStreams = true
}
